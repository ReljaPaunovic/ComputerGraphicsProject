varying vec3 coordinates;
varying vec3 normal;

uniform float cameraX;

uniform float screenWidth;
uniform float screenHeight;

vec3 mod289(vec3 x) {
  return x - floor(x * (1.0 / 289.0)) * 289.0;
}

vec2 mod289(vec2 x) {
  return x - floor(x * (1.0 / 289.0)) * 289.0;
}

vec3 permute(vec3 x) {
  return mod289(((x*34.0)+1.0)*x);
}

float snoise(vec2 v)
  {
  const vec4 C = vec4(0.211324865405187,  // (3.0-sqrt(3.0))/6.0
                      0.366025403784439,  // 0.5*(sqrt(3.0)-1.0)
                     -0.577350269189626,  // -1.0 + 2.0 * C.x
                      0.024390243902439); // 1.0 / 41.0
// First corner
  vec2 i  = floor(v + dot(v, C.yy) );
  vec2 x0 = v -   i + dot(i, C.xx);

// Other corners
  vec2 i1;
  //i1.x = step( x0.y, x0.x ); // x0.x > x0.y ? 1.0 : 0.0
  //i1.y = 1.0 - i1.x;
  i1 = (x0.x > x0.y) ? vec2(1.0, 0.0) : vec2(0.0, 1.0);
  // x0 = x0 - 0.0 + 0.0 * C.xx ;
  // x1 = x0 - i1 + 1.0 * C.xx ;
  // x2 = x0 - 1.0 + 2.0 * C.xx ;
  vec4 x12 = x0.xyxy + C.xxzz;
  x12.xy -= i1;

// Permutations
  i = mod289(i); // Avoid truncation effects in permutation
  vec3 p = permute( permute( i.y + vec3(0.0, i1.y, 1.0 ))
		+ i.x + vec3(0.0, i1.x, 1.0 ));

  vec3 m = max(0.5 - vec3(dot(x0,x0), dot(x12.xy,x12.xy), dot(x12.zw,x12.zw)), 0.0);
  m = m*m ;
  m = m*m ;

// Gradients: 41 points uniformly over a line, mapped onto a diamond.
// The ring size 17*17 = 289 is close to a multiple of 41 (41*7 = 287)

  vec3 x = 2.0 * fract(p * C.www) - 1.0;
  vec3 h = abs(x) - 0.5;
  vec3 ox = floor(x + 0.5);
  vec3 a0 = x - ox;

// Normalise gradients implicitly by scaling m
// Approximation of: m *= inversesqrt( a0*a0 + h*h );
  m *= 1.79284291400159 - 0.85373472095314 * ( a0*a0 + h*h );

// Compute final noise value at P
  vec3 g;
  g.x  = a0.x  * x0.x  + h.x  * x0.y;
  g.yz = a0.yz * x12.xz + h.yz * x12.yw;
  return 130.0 * dot(m, g);
}

vec3 transformPos(vec2 noiseCoords) {
	return vec3(
		noiseCoords.x,
		snoise(noiseCoords) * 0.3 + (1.0 - noiseCoords.y) / 3.0 - 0.7,
		noiseCoords.y
	);
}

vec3 calculateNormal(vec2 noiseCoords) {
	vec3 p1 = transformPos(noiseCoords);
	vec3 p2 = transformPos(noiseCoords + vec2(0.02, 0.0));
	vec3 p3 = transformPos(noiseCoords + vec2(0.0, 0.02));

	return -normalize(cross(p2 - p1, p3 - p1));
}

void main() {
	vec2 noiseCoords = vec2(gl_Vertex.x + cameraX / screenWidth, gl_Vertex.z);
    coordinates = transformPos(noiseCoords);
    coordinates.x -= cameraX / screenWidth;

	normal = calculateNormal(noiseCoords);

	gl_Position = gl_ModelViewProjectionMatrix * vec4(coordinates, 1.0);
	gl_TexCoord[0] = vec4(gl_MultiTexCoord0.x + cameraX / (2.0 * screenWidth), gl_MultiTexCoord0.yzw);
}
